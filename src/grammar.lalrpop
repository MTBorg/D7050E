grammar<'a>();

use std::{
	str::FromStr,
	collections::HashMap
};

use crate::{
	types::{
		opcode::Opcode,
		node::Node,
		func::Func,
		func_param::FuncParam,
		_type::Type
	}
};

pub File: HashMap<&'a str, Func<'a>> = {
	<mut func_decs:FuncDec+> => {
		let mut hmap = HashMap::new();
		for func_dec in func_decs.drain(..){
			hmap.insert(func_dec.name, func_dec);
		}
		hmap
	}
};
	

Identifier: String = {
	r"_*[a-z]+\w*" => <>.to_string()
}

Expr: Box<Node<'a>> = {
    Expr ExprOp Factor => Box::new(Node::Op(<>)),
    Expr LogOp Factor => Box::new(Node::Op(<>)),
    Factor,
};

LogOp: Opcode = {
	"&&" => Opcode::And,
	"||" => Opcode::Or,
};

ExprOp: Opcode = { 
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

Factor: Box<Node<'a>> = {
    Factor FactorOp Term => Box::new(Node::Op(<>)),
    Factor RelOp Term => Box::new(Node::Op(<>)),
    Term,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

RelOp: Opcode = {
	"==" => Opcode::Eq,
	"!=" => Opcode::Neq,
	">=" => Opcode::Geq,
	"<=" => Opcode::Leq,
	">" => Opcode::Gneq,
	"<" => Opcode::Lneq
};

Term: Box<Node<'a>> = {
    Num, 
		Var,
		Bool,
		FuncCall,
    "(" <Expr> ")"
};

Num: Box<Node<'a>> = {
    r"-?[0-9]+" => Box::new(Node::Number(i32::from_str(<>).unwrap()))
};

Var: Box<Node<'a>> = {
	Identifier => Box::new(Node::Var(&<>))
};

FuncCall: Box<Node<'a>> = {
	<func_name:Identifier> "(" <args:FuncArgs> ")" =>	
		Box::new(
			Node::FuncCall(
				&func_name,
				args,
				None
			)
		)
};

FuncArgs: Vec<Node<'a>> = {
	<args:(<FuncArg> ",")*> <arg:FuncArg?> => match arg{
		Some(arg) => {
			let mut args = args;
			args.push(arg);
			args
		},
		None => args
	}
};

FuncArg: Node<'a> = {
	Expr => *<>
};
	
FuncName = Identifier;

FuncParam: FuncParam<'a> = {
	<name:Identifier> ":" <mutable:"mut"?> <_type:Type> => FuncParam{
		name: &name,
		_type: _type,
		mutable: match mutable{
			Some(_) => true,
			None => false
		}
	}
};

FuncParams: Vec<FuncParam<'a>> = {
	<params:(<FuncParam> ",")*> <param:FuncParam?> => match param{
		Some(param) => {
			let mut params = params;
			params.push(param);
			params
		},
		None => params
	}
};

FuncDec: Func<'a> = {
	"fn" <name:FuncName> "(" <params:FuncParams> ")" <ret_type:("->" <Type>)?> <body_start:Body> =>
	Func{
		name: &name,
		params: params,
		ret_type: ret_type,
		body_start: *body_start
	}
};

Assign: Box<Node<'a>> = {
	<id:Identifier> "=" <expr:Expr> ";" =>
		Box::new(
			Node::Assign(
			&id,
			expr,
			None
			)
		)
};

Let: Box<Node<'a>> = {
	"let" <mutable:"mut"?> <id:Identifier> <_type:(":" <Type>)?> "=" <expr:Expr> ";"=> 
		Box::new(
			Node::Let(
				&id,
				_type,
				if let Some(_) = mutable {true} else {false},
				expr,
				None
			)
		),
};

Instruction: Box<Node<'a>> = {
	Let,
	Assign,
	If,
	<FuncCall> ";" => <>,
	Return,
	Debug_Context,
	Print
};

Instructions: Box<Node<'a>> = {
	<mut i1:Instruction> <i2:Instructions?> => match i2{
		Some(node) => {
			i1.attach_right_most_child(*node);
			i1
		},
		None => {
			i1
		}
	}
};

Body: Box<Node<'a>> = {
	"{" <Instructions?> "}" => match <>{
		Some(node) => node,
		None => Box::new(Node::Empty)
	}
};

If: Box<Node<'a>> = {
	"if" <expr:Expr> <body:Body> => Box::new(
		Node::If(
			expr,
			body,
			None,
			None
		)
	),
	"if" <expr:Expr> <then_body:Body> "else" <else_body:Body> => Box::new(
			Node::If(
				expr,
				then_body,
				Some(else_body),
				None
			)
	)
};

Bool: Box<Node<'a>> = {
	"true" => Box::new(Node::Bool(true)),
	"false" => Box::new(Node::Bool(false))
};

Type: Type = {
	"bool" => Type::Bool,
	"i32" => Type::Int,
}

Return: Box<Node<'a>> = {
	"return" <Expr> ";" => Box::new(Node::Return(<>, None)),
}

Print: Box<Node<'a>> = {
	"print " <Expr> => Box::new(Node::Print(<>, None))
};
Debug_Context: Box<Node<'a>> = {
	"$DEBUG_CONTEXT" => Box::new(Node::DebugContext(None))
};
