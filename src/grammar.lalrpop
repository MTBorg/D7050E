grammar;

use std::str::FromStr;

use crate::{
	parsing::expr_parser::Opcode,
	node::Node,
	func::FuncDec,
	func_param::FuncParam,
};


Identifier: String = {
	r"_*[a-z]+\w*" => <>.to_string()
}

pub Expr: Box<Node> = {
    Expr ExprOp Factor => Box::new(Node::Op(<>)),
    Factor,
};

ExprOp: Opcode = { 
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

Factor: Box<Node> = {
    Factor FactorOp Term => Box::new(Node::Op(<>)),
    Term,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

Term: Box<Node> = {
    Num => Box::new(Node::Number(<>)), 
		Var => Box::new(Node::Var(<>)),
    "(" <Expr> ")"
};

Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};

Var = Identifier;

pub FuncCall: Box<Node> = {
	<func_name:Identifier> "(" <args:FuncArgs> ")" ";"? =>	
		Box::new(
			Node::FuncCall(
				func_name,
				args,
				None
			)
		)
};

FuncArgs: Vec<String> = {
	<args:(<FuncArg> ",")*> <arg:FuncArg?> => match arg{
		Some(arg) => {
			let mut args = args;
			args.push(arg);
			args
		},
		None => args
	}
};

FuncArg: String = {
	Identifier,
	Num => <>.to_string()
};
	
FuncName = Identifier;

FuncParam: FuncParam = {
	<name:Identifier> ":" <mutable:"&"?> <_type:Type> => FuncParam{
		name: name,
		_type: _type.to_string(),
		mutable: match mutable{
			Some(_) => true,
			None => false
		}
	}
};

FuncParams: Vec<FuncParam> = {
	<params:(<FuncParam> ",")*> <param:FuncParam?> => match param{
		Some(param) => {
			let mut params = params;
			params.push(param);
			params
		},
		None => params
	}
};

pub FuncDec: FuncDec = {
	"fn" <name:FuncName> "(" <params:FuncParams> ")" <ret_type:("->" <Type>)?> "{}" => FuncDec{
		name: name,
		params: params,
		ret_type: match ret_type{
			Some(ret_type) => ret_type,
			None => "".to_string()
		}
	}
};

pub Let: Box<Node> = {
	"let" <id:Identifier> <_type:(":" <Type>)?> "=" <expr:Expr> => 
		Box::new(
			Node::Let(
				Box::new(Node::Var(id)),
				expr,
				None
			)
		),
};

Instruction: Box<Node> = {
	<Let> ";" => <>,
	<If> => <>
};

Instructions: Box<Node> = {
	<mut i1:Instruction> <i2:Instructions?> => match i2{
		Some(node) => {
			i1.attach_right_most_child(*node);
			i1
		},
		None => {
			i1
		}
	}
};

pub Body: Box<Node> = {
	"{" <Instructions> "}" => <>
};

pub If: Box<Node> = {
	"if" <expr:Expr> <body:Body> => Box::new(
		Node::If(
			expr,
			body,
			None,
			None
		)
	),
	"if" <expr:Expr> <then_body:Body> "else" <else_body:Body> => Box::new(
			Node::If(
				expr,
				then_body,
				Some(else_body),
				None
			)
	)
};

// TODO: This should use a type enum instead
Type: String = {
	"bool" => <>.to_string(), 
	"char" => <>.to_string(), 
	"i8" => <>.to_string(), 
	"i16" => <>.to_string(), 
	"i32" => <>.to_string(), 
	"i64" => <>.to_string(), 
	"isize" => <>.to_string(), 
	"u8" => <>.to_string(), 
	"u16" => <>.to_string(), 
	"u32" => <>.to_string(), 
	"u64" => <>.to_string(), 
	"usize" => <>.to_string(), 
	"f32" => <>.to_string(), 
	"f64" => <>.to_string(), 
	"array" => <>.to_string(), 
	"slice" => <>.to_string(), 
	"str" => <>.to_string(), 
	"tuple" => <>.to_string(), 
}
